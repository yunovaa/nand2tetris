// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not
/*

*/
CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zx-> And(not(zx),x) --> Mux(x, And(Not(x), x), zx) --> outzx
        Not16(in=x, out=notx );
        And16(a=notx , b=x , out=Xzr);
        Mux16(a=x , b=Xzr , sel=zx , out=outzx );
    // zy-> And(not(zy),y) --> Mux(y, And(Not(y), y), zy) --> outzy
        Not16(in=y, out=noty );
        And16(a=noty , b=y , out=Yzr);
        Mux16(a=y , b=Yzr , sel=zy , out=outzy );
    // nx-> Xor(outzx, nx) --> Mux(outzx, Not(outzx), nx) --> outnx
        Not16(in=outzx , out=notoutzx );
        Mux16(a=outzx , b=notoutzx , sel=nx , out=outnx );
    // ny-> Xor(outzy, ny) --> Mux(outzy, Not(outzy), ny) --> outny
        Not16(in=outzy , out=notoutzy );
        Mux16(a=outzy , b=notoutzy , sel=ny , out=outny );
    // f -> ADD(outny, outnx) = ADDxy,, And(outnx, outny) = Andxy
    // Mux(ADDxy, Andxy, f) ---> outf
        Add16(a=outnx , b=outny , out=addxy);
        And16(a=outnx , b=outny , out=Andxy );
        Mux16(a=Andxy, b=addxy, sel=f , out= outf);
    // no -> Xor(outf, no) ---> Mux(outf, And(Not(outf),outf),no) -> out
    // ng -> out[15]
        Not16(in=outf , out=notoutf );
        Mux16(a=outf, b=notoutf , sel=no , out=out, out[15]=ng, out[0..7]=out0to7, out[8..15]=out8to16);
    //zr 
        Or8Way(in=out0to7 , out=out1 );
        Or8Way(in=out8to16 , out=out2 );
        Or(a=out1 , b=out2, out=out3 );
        Not(in=out3 , out=zr );
}