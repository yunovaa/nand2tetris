// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
        Mux16(a=instruction , b=outALU , sel=instruction[15] , out=outA );
        Mux(a=true , b=instruction[5] , sel=instruction[15] , out=loadA );
        ARegister(in=outA , load=loadA , out=outAregister, out[0..14]=addressM );
        And(a=instruction[15] , b=instruction[12] , out=loadAorM );
        Mux16(a=outAregister , b=inM , sel=loadAorM , out=inpALUAorM );
        And(a=instruction[15], b=instruction[4] , out= loadD);
        DRegister(in=outALU , load=loadD , out=outDregister);
        ALU(x=outDregister , y=inpALUAorM , zx=instruction[11] , nx=instruction[10] , zy=instruction[9] , ny=instruction[8] , f=instruction[7] , no=instruction[6] , out=outALU , zr=zr , ng=ng, out=outM );
        And(a=instruction[15] , b=instruction[3] , out=writeM );
        And(a=instruction[2] , b=ng , out=outjump1 );
        And(a=instruction[1] , b=zr , out=outjump2 );
        Or(a=ng , b=zr , out=p );
        Not(in=p , out=notp );
        And(a=instruction[0] , b=notp , out=outjump3 );
        Or(a=outjump1 , b=outjump2 , out=loadPC1 );
        Or(a=loadPC1 , b=outjump3 , out=loadPC2 );
        And(a=loadPC2 , b=instruction[15] , out=loadPC );
        //Not(in=loadPC , out=notloadPC );
        PC(in=outAregister , load=loadPC , inc=true , reset=reset , out[0..14]=pc );

}